// Test EngineRender Module Script
int GetTestEngineRenderCount()
{
  return 8;
}

void _FillSprite(DynamicSprite *spr, int color)
{
  DrawingSurface *ds = spr.GetDrawingSurface();
  ds.Clear(color);
  ds.Release();
}

// Takes screenshot 1:1 and compares a part of the image with the given sprite
bool _TakeScreenshotAndCompareRect(int rx, int ry, int rw, int rh, DynamicSprite *match_with)
{
  DynamicSprite *shot = DynamicSprite.CreateFromScreenShot(Screen.Width, Screen.Height);
  DrawingSurface *test_ds = shot.GetDrawingSurface();
  DrawingSurface *match_ds = match_with.GetDrawingSurface();
  
  for (int y = 0; y < rh; y++) {
    for (int x = 0; x < rw; x++) {
      if (test_ds.GetPixel(x + rx, y + ry) != match_ds.GetPixel(x, y)) {
        return false;
      }
    }
  }
  
  return true;
}

void TestEngineRender()
{  
  tap.Comment("start EngineRender tests");
  tap.Comment(String.Format("System.HardwareAcceleration = %d", System.HardwareAcceleration));
  
  //
  // Object texture update after dynamic sprite change
  //
  {
    int color1 = 10;
    int color2 = 11;
    Object* oDynamicObject = object[0];
    DynamicSprite* dynspr = DynamicSprite.Create(10, 10, false);
    _FillSprite(dynspr, color1);
    oDynamicObject.Graphic = dynspr.Graphic;
    oDynamicObject.X = 100;
    oDynamicObject.Y = 100;
    oDynamicObject.Visible = true;
    Wait(1);
    tap.ok(
      _TakeScreenshotAndCompareRect(oDynamicObject.X, oDynamicObject.Y - dynspr.Height, dynspr.Width, dynspr.Height, dynspr), 
      "DynamicSprite update 1-1 (room object) is matched");
    
    
    _FillSprite(dynspr, color2);
    Wait(1);
    tap.ok(
      _TakeScreenshotAndCompareRect(oDynamicObject.X, oDynamicObject.Y - dynspr.Height, dynspr.Width, dynspr.Height, dynspr), 
      "DynamicSprite update 1-2 (room object) is matched");
    
    oDynamicObject.Visible = false;
    oDynamicObject.Graphic = 0;
    dynspr.Delete();
  }
  
  //
  // Object texture update after dynamic sprite recreated
  // with coincidentally the same sprite ID
  //
  {
    int color1 = 10;
    int color2 = 11;
    DynamicSprite* dynspr = DynamicSprite.Create(10, 10, false);
    Object* oDynamicObject = object[1];
    _FillSprite(dynspr, color1);
    oDynamicObject.Graphic = dynspr.Graphic;
    oDynamicObject.X = 100;
    oDynamicObject.Y = 100;
    oDynamicObject.Visible = true;
    Wait(1);
    tap.ok(
      _TakeScreenshotAndCompareRect(oDynamicObject.X, oDynamicObject.Y - dynspr.Height, dynspr.Width, dynspr.Height, dynspr), 
      "DynamicSprite recreate 1-1 (room object) is matched");
    
    dynspr.Delete();
    dynspr = DynamicSprite.Create(10, 10, false);
    _FillSprite(dynspr, color2);
    oDynamicObject.Graphic = dynspr.Graphic;
    Wait(1);
    tap.ok(
      _TakeScreenshotAndCompareRect(oDynamicObject.X, oDynamicObject.Y - dynspr.Height, dynspr.Width, dynspr.Height, dynspr), 
      "DynamicSprite recreate 1-2 (room object) is matched");
    
    oDynamicObject.Visible = false;
    oDynamicObject.Graphic = 0;
    dynspr.Delete();
  }
  
  //
  // Overlay texture update after dynamic sprite change;
  // We test overlays separately, because they have slightly different
  // path of updating their textures in the engine. Also they are
  // dynamic objects themselves and may be deleted completely.
  //
  {
    int color1 = 10;
    int color2 = 11;
    DynamicSprite* dynspr = DynamicSprite.Create(10, 10, false);
    _FillSprite(dynspr, color1);
    Overlay* over = Overlay.CreateGraphical(100, 100, dynspr.Graphic);
    Wait(1);
    tap.ok(
      _TakeScreenshotAndCompareRect(over.X, over.Y, dynspr.Width, dynspr.Height, dynspr), 
      "DynamicSprite update 2-1 (overlay) is matched");
    
    
    _FillSprite(dynspr, color2);
    Wait(1);
    tap.ok(
      _TakeScreenshotAndCompareRect(over.X, over.Y, dynspr.Width, dynspr.Height, dynspr), 
      "DynamicSprite update 2-2 (overlay) is matched");
    
    over.Remove();
    dynspr.Delete();
  }
  
  //
  // Overlay texture update after dynamic sprite recreated
  // with coincidentally the same sprite ID
  //
  {
    int color1 = 10;
    int color2 = 11;
    DynamicSprite* dynspr = DynamicSprite.Create(10, 10, false);
    _FillSprite(dynspr, color1);
    Overlay* over = Overlay.CreateGraphical(100, 100, dynspr.Graphic);
    Wait(1);
    tap.ok(
      _TakeScreenshotAndCompareRect(over.X, over.Y, dynspr.Width, dynspr.Height, dynspr), 
      "DynamicSprite recreate 2-1 (overlay) is matched");
    
    dynspr.Delete();
    dynspr = DynamicSprite.Create(10, 10, false);
    _FillSprite(dynspr, color2);
    over.Graphic = dynspr.Graphic;
    Wait(1);
    tap.ok(
      _TakeScreenshotAndCompareRect(over.X, over.Y, dynspr.Width, dynspr.Height, dynspr), 
      "DynamicSprite recreate 2-2 (overlay) is matched");
    
    over.Remove();
    dynspr.Delete();
  }
  
  tap.Comment("end EngineRender tests");
}
