// Test Objects Module Script
int GetTestObjectsCount()
{
  return 1 // graphic pos
  + 8 // view frames
  ;
}

#define SPRITE_DEFAULT 7

void _TestObjectRenderF(Object *testObject, int sprite_num, eFlipDirection flip, const string test_name)
{
  Wait(1); // let engine redraw
  int sprite_w = Game.SpriteWidth[sprite_num];
  int sprite_h = Game.SpriteHeight[sprite_num];
  int sprite_x = testObject.X;
  int sprite_y = testObject.Y - sprite_h + 1;
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(sprite_x, sprite_y, sprite_w, sprite_h, sprite_num, flip), test_name);
}

void _TestObjectRender(Object *testObject, int sprite_num, const string test_name)
{
  _TestObjectRenderF(testObject, sprite_num, eFlipNone, test_name);
}

void _TestObjectViewFrame(Object *testObject, int view, int loop, int frame, const string test_name)
{
  testObject.SetView(view, loop, frame);
  ViewFrame* vf = Game.GetViewFrame(view, loop, frame);
  _TestObjectRenderF(testObject, vf.Graphic, vf.Flipped, test_name);
}

void TestGraphicPosition(Object *testObject)
{
  testObject.Graphic = SPRITE_DEFAULT;
  _TestObjectRender(testObject, SPRITE_DEFAULT, "Object: graphic position");
}

void TestViews(Object *testObject)
{
  _TestObjectViewFrame(testObject, VFLIP, 0, 0, "Object: view frame 0, normal");
  _TestObjectViewFrame(testObject, VFLIP, 0, 1, "Object: view frame 1, normal");
  _TestObjectViewFrame(testObject, VFLIP, 0, 2, "Object: view frame 2, h-flip");
  _TestObjectViewFrame(testObject, VFLIP, 0, 3, "Object: view frame 3, h-flip");
  _TestObjectViewFrame(testObject, VFLIP, 0, 4, "Object: view frame 4, v-flip");
  _TestObjectViewFrame(testObject, VFLIP, 0, 5, "Object: view frame 5, v-flip");
  _TestObjectViewFrame(testObject, VFLIP, 0, 6, "Object: view frame 6, hv-flip");
  _TestObjectViewFrame(testObject, VFLIP, 0, 7, "Object: view frame 7, hv-flip");
}

void TestObjects()
{
  tap.Comment("start Object tests");
  tap.Comment(String.Format("System.HardwareAcceleration = %d", System.HardwareAcceleration));
  
  Object* oTestObject = object[0];
  oTestObject.X = 100;
  oTestObject.Y = 100;
  oTestObject.Visible = true;
  oTestObject.Transparency = 0;
  
  TestGraphicPosition(oTestObject);
  TestViews(oTestObject);
  
  tap.Comment("end View tests");
}
