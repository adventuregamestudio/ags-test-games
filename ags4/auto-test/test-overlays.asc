// Test Overlays Module Script
int GetTestOverlaysCount()
{
  return 0
  + 4 * 4   // overlay creation tests (x types)
  + 1 * 4   // 3.6.3 overlay creation tests (x types)
  + 6 * 2   // basic properties (x types)
  + 6 * 2   // 3.6.2 basic properties (x types)
  + 3 * 2   // 3.6.3 basic properties (x types)
  + 20 * 2  // 4.0 basic properties (x types)
  + 3       // graphic properties
  //--------------------------------------------------
  + 6 * 4   // animated overlay creation tests
  + 35      // animated overlay basic properties
  + 3       // animated overlay graphic properties
  + 4       // animated overlay extended properties
  //--------------------------------------------------
  + 2       // graphic pos (graphic overlay)
  + 3       // graphic pos (textual overlay)
  + 1       // graphic pos (animated overlay)
  + 8       // view frames (animated overlay)
  ;
}

#define SPRITE_DEFAULT 7

#define OVERLAY_TEXTUAL_COLOR_1 0xFF3104CE
#define OVERLAY_TEXTUAL_COLOR_2 0xFFD6868C

#define OVERLAY_SHOT_TEXTUAL_1 9
#define OVERLAY_SHOT_TEXTUAL_2 10

void _TestOverlayCreation(Overlay* over, bool in_room, bool is_graphical, String over_name)
{
  tap.is_int(over.Valid, true, String.Format("Overlay.Create(%s): is valid", over_name));
  if (in_room)
    tap.is_int(over.InRoom, true, String.Format("Overlay.Create(%s): is room overlay", over_name));
  else
    tap.is_int(over.InRoom, false, String.Format("Overlay.Create(%s): is screen overlay", over_name));
  // NOTE: textual overlays also have a Graphic, because they generate one from text
  tap.is_int(over.Graphic > 0, true, String.Format("Overlay.Create(%s): has graphic", over_name));
  // 3.6.3 properties
  tap.is_int(over.Visible, true, String.Format("Overlay.Create(%s): is visible", over_name));
}

void _TestOverlayRemoval(Overlay* over, String over_name)
{
  over.Remove();
  tap.is_int(over.Valid, false, String.Format("Overlay.Remove(%s): is removed", over_name));
}

void _TestOverlayCreationAndRemoval(Overlay* over, bool in_room, bool is_graphical, String over_name)
{
  _TestOverlayCreation(over, in_room, is_graphical, over_name);
  _TestOverlayRemoval(over, over_name);
}

void TestOverlayCreation()
{
  _TestOverlayCreationAndRemoval(Overlay.CreateGraphical(100, 100, SPRITE_DEFAULT), false, true, "graphical screen");
  _TestOverlayCreationAndRemoval(Overlay.CreateRoomGraphical(100, 100, SPRITE_DEFAULT), true, true, "graphical room");
  _TestOverlayCreationAndRemoval(Overlay.CreateTextual(100, 100, 100, eFontFont0, 0, "x"), false, false, "textual screen");
  _TestOverlayCreationAndRemoval(Overlay.CreateRoomTextual(100, 100, 100, eFontFont0, 0, "x"), true, false, "textual room");
}

void TestOverlayBaseProperties(Overlay* over, String createdWithText, String over_name)
{
  // Properties
  over.Height = 144;
  tap.is_int(over.Height, 144, String.Format("%s.Height", over_name));
  over.Transparency = 50;
  tap.is_int(over.Transparency, 50, String.Format("%s.Transparency", over_name));
  over.Width = 156;
  tap.is_int(over.Width, 156, String.Format("%s.Width", over_name));
  over.X = 77;
  tap.is_int(over.X, 77, String.Format("%s.X", over_name));
  over.Y = 99;
  tap.is_int(over.Y, 99, String.Format("%s.Y", over_name));
  over.ZOrder = 111;
  tap.is_int(over.ZOrder, 111, String.Format("%s.ZOrder", over_name));
  
  // 3.6.3 Properties
  tap.is(over.Text, createdWithText, String.Format("%s.Text: initial", over_name));
  over.Text = "custom";
  tap.is(over.Text, "custom", String.Format("%s.Text: set", over_name));
  over.Visible = false;
  tap.is_int(over.Visible, false, String.Format("%s.Visible", over_name));
  
  // 3.6.2 Functions
  over.SetPosition(1, 2, 40, 50);
  tap.is_int(over.X, 1, String.Format("%s.SetPosition(): x", over_name));
  tap.is_int(over.Y, 2, String.Format("%s.SetPosition(): y", over_name));
  tap.is_int(over.Width, 40, String.Format("%s.SetPosition(): width", over_name));
  tap.is_int(over.Height, 50, String.Format("%s.SetPosition(): height", over_name));
  over.SetSize(60, 70);
  tap.is_int(over.Width, 60, String.Format("%s.SetSize(): width", over_name));
  tap.is_int(over.Height, 70, String.Format("%s.SetSize(): height", over_name));
  
  // 4.0 Properties
  over.BlendMode = eBlendDodge;
  tap.is_int(over.BlendMode, eBlendDodge, String.Format("%s.BlendMode", over_name));
  over.Flip = eFlipUpsideDown;
  tap.is_int(over.Flip, eFlipUpsideDown, String.Format("%s.Flip", over_name));
  tap.is_int(over.HasLightLevel, false, String.Format("%s.HasLightLevel", over_name));
  tap.is_int(over.HasTint, false, String.Format("%s.HasTint", over_name));
  over.Height = 144;
  tap.is_int(over.Height, 144, String.Format("%s.Height", over_name));
  tap.is_int(over.LightLevel, 0, String.Format("%s.LightLevel", over_name));
  over.Rotation = 45.0;
  tap.is_float(over.Rotation, 45.0, 0.0001, String.Format("%s.Rotation", over_name));
  tap.is_int(over.TintBlue, 0, String.Format("%s.TintBlue", over_name));
  tap.is_int(over.TintGreen, 0, String.Format("%s.TintGreen", over_name));
  tap.is_int(over.TintLuminance, 0, String.Format("%s.TintLuminance", over_name));
  tap.is_int(over.TintRed, 0, String.Format("%s.TintRed", over_name));
  tap.is_int(over.TintSaturation, 0, String.Format("%s.TintSaturation", over_name));
  
  // 4.0 Functions
  over.SetLightLevel(40);
  tap.is_int(over.HasLightLevel, true, String.Format("%s.SetLightLevel(): HasLightLevel", over_name));
  tap.is_int(over.LightLevel, 40, String.Format("%s.SetLightLevel(): LightLevel", over_name));
  
  over.Tint(255, 192, 128, 96, 64);
  tap.is_int(over.HasTint, true, String.Format("%s.Tint(): HasTint", over_name));
  tap.is_int(over.TintBlue, 128, String.Format("%s.Tint(): TintBlue", over_name));
  tap.is_int(over.TintGreen, 192, String.Format("%s.Tint(): TintGreen", over_name));
  tap.is_int(over.TintLuminance, 64, String.Format("%s.Tint(): TintLuminance", over_name));
  tap.is_int(over.TintRed, 255, String.Format("%s.Tint(): TintRed", over_name));
  tap.is_int(over.TintSaturation, 96, String.Format("%s.Tint(): TintSaturation", over_name));
}

void TestOverlayGraphicProperties(Overlay* over, String over_name)
{
  over.Graphic = 4;
  tap.is_int(over.Graphic, 4, String.Format("%s.Graphic", over_name));
  tap.is_int(over.GraphicHeight, Game.SpriteHeight[4], String.Format("%s.GraphicHeight", over_name));
  tap.is_int(over.GraphicWidth, Game.SpriteWidth[4], String.Format("%s.GraphicWidth", over_name));
}

void TestGraphicOverlay()
{
  Overlay* over = Overlay.CreateGraphical(100, 100, 0);
  TestOverlayBaseProperties(over, "", "Graphic Overlay");
  TestOverlayGraphicProperties(over, "Graphic Overlay");
}

void TestTextualOverlay()
{
  Overlay* over = Overlay.CreateTextual(100, 100, 100, eFontFont0, 12345, "some string to display");
  TestOverlayBaseProperties(over, "some string to display", "Textual Overlay");
}

void _TestAnimatedOverlayCreationAndRemoval(AnimatedOverlay* over, bool in_room, bool is_graphical, bool is_pausing, String over_name)
{
  _TestOverlayCreation(over, in_room, is_graphical, over_name);
  tap.is_int(over.PauseWithGame, is_pausing, String.Format("AnimatedOverlay.Create(%s): pause with game", over_name));
  _TestOverlayRemoval(over, over_name);
}

void TestAnimatedOverlayCreation()
{
  _TestAnimatedOverlayCreationAndRemoval(AnimatedOverlay.CreateAnimated(100, 100, SPRITE_DEFAULT, false), false, true, false, "animated screen");
  _TestAnimatedOverlayCreationAndRemoval(AnimatedOverlay.CreateAnimated(100, 100, SPRITE_DEFAULT, true), false, true, true, "animated screen (pausing)");
  _TestAnimatedOverlayCreationAndRemoval(AnimatedOverlay.CreateRoomAnimated(100, 100, SPRITE_DEFAULT, false), true, true, false, "animated room");
  _TestAnimatedOverlayCreationAndRemoval(AnimatedOverlay.CreateRoomAnimated(100, 100, SPRITE_DEFAULT, true), true, true, true, "animated room (pausing)");
}

void TestAnimatedOverlayProperties(AnimatedOverlay* over, String over_name)
{
  tap.is_int(over.Animating, false, String.Format("%s.Animating", over_name));
  tap.is_int(over.Frame, 0, String.Format("%s.Frame", over_name));
  tap.is_int(over.Loop, 0, String.Format("%s.Loop", over_name));
  tap.is_int(over.View, 0, String.Format("%s.View", over_name));
}

void TestAnimatedOverlay()
{
  AnimatedOverlay* aover = AnimatedOverlay.CreateAnimated(100, 100, 0);
  TestOverlayBaseProperties(aover, "", "AnimatedOverlay");
  TestOverlayGraphicProperties(aover, "AnimatedOverlay");
  TestAnimatedOverlayProperties(aover, "AnimatedOverlay");
}

//int test_render_num;
void _TestObjectRenderF(Overlay *testObject, int sprite_num, eFlipDirection flip, String test_name)
{
  Wait(1); // let engine redraw
  int sprite_w = Game.SpriteWidth[sprite_num];
  int sprite_h = Game.SpriteHeight[sprite_num];
  int sprite_x = testObject.X;
  int sprite_y = testObject.Y;
  //tap.Comment(String.Format("sprite_x = %d, sprite_y = %d, sprite_w = %d, sprite_h = %d", sprite_x, sprite_y, sprite_w, sprite_h));
  //SaveScreenShot(String.Format("_overlay_%d.bmp", test_render_num));
  //test_render_num++;
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(sprite_x, sprite_y, sprite_w, sprite_h, sprite_num, flip), test_name);
}

void _TestObjectRender(Overlay *testObject, int sprite_num, String test_name)
{
  _TestObjectRenderF(testObject, sprite_num, eFlipNone, test_name);
}

void _TestObjectViewFrame(Overlay *testObject, int view, int loop, int frame, String test_name)
{
  ViewFrame* vf = Game.GetViewFrame(view, loop, frame);
  _TestObjectRenderF(testObject, vf.Graphic, vf.Flipped, test_name);
}

void TestGraphicPosition(Overlay *testObject, String overlay_name)
{
  testObject.Graphic = SPRITE_DEFAULT;
  _TestObjectRender(testObject, SPRITE_DEFAULT, String.Format("%s: graphic position", overlay_name));
}

void TestOverlayRender()
{
  // Graphical overlay
  Overlay* over = Overlay.CreateGraphical(100, 100, 0);
  TestGraphicPosition(over, "Overlay");
  over.SetPosition(120, 120);
  TestGraphicPosition(over, "Overlay.SetPosition");
  over.Remove();
  
  // Textual overlay
  over = Overlay.CreateTextual(100, 100, 100, eFontFont0, OVERLAY_TEXTUAL_COLOR_1, "some string to display");
  Wait(1);
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(100, 100, 100, 24, OVERLAY_SHOT_TEXTUAL_1), "Overlay.CreateTextual: visual");
  
  over.SetPosition(120, 120);
  Wait(1);
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(120, 120, 100, 24, OVERLAY_SHOT_TEXTUAL_1), "Overlay.SetPosition: visual");
  
  over.SetText(100, eFontFont0, OVERLAY_TEXTUAL_COLOR_2, "another string to display");
  Wait(1);
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(120, 120, 100, 24, OVERLAY_SHOT_TEXTUAL_2), "Overlay.SetText: visual");
  
  over.Remove();
}

void TestViewRender(AnimatedOverlay *testObject, String overlay_name)
{
  // NOTE: add some animation delay, because our TestRender function also does Wait(1) before screenshot
  testObject.Animate(VFLIP, 0, 2, eOnce, eNoBlock, eForwards);
  while (testObject.Animating)
  {
    int frame = testObject.Frame;
    String test_name = String.Format("%s: view frame %d, ", overlay_name, frame);
    switch (frame)
    {
    case 0:
    case 1:
      test_name = test_name.Append("normal");
      break;
    case 2:
    case 3:
      test_name = test_name.Append("h-flip");
      break;
    case 4:
    case 5:
      test_name = test_name.Append("v-flip");
      break;
    case 6:
    case 7:
      test_name = test_name.Append("hv-flip");
      break;
    }
    _TestObjectViewFrame(testObject, VFLIP, 0, frame, test_name);
    
    while (testObject.Frame == frame)
    {
      Wait(1);
    }
  }
}

void TestAnimatedOverlayRender()
{
  AnimatedOverlay* aover = AnimatedOverlay.CreateAnimated(100, 100, 0);
  TestGraphicPosition(aover, "AnimatedOverlay");
  TestViewRender(aover, "AnimatedOverlay");
  aover.Remove();
}

void TestOverlays()
{
  tap.Comment("start Overlays tests");
  tap.Comment(String.Format("System.HardwareAcceleration = %d", System.HardwareAcceleration));
  
  // Disable characters and objects, avoid interfering with our tests
  TestHelpers.HideRoomObjects();
  TestHelpers.HideGUIs();
  
  //------------------------------------------------------------
  // Basic Overlays
  //------------------------------------------------------------
  // Test overlay creation
  TestOverlayCreation();
  // Test graphic overlay's properties and functions
  TestGraphicOverlay();
  // Test textual overlay's properties and functions
  TestTextualOverlay();
  // Test rendering
  TestOverlayRender();

  //------------------------------------------------------------
  // Animated Overlays
  //------------------------------------------------------------
  // Test animated overlay creation
  TestAnimatedOverlayCreation();
  // Test animated overlay properties
  TestAnimatedOverlay();
  // Test rendering
  TestAnimatedOverlayRender();
  
  tap.Comment("end Overlays tests");
}
