// Test Overlays Module Script
int GetTestOverlaysCount()
{
  return 1 // graphic pos
  + 8 // view frames
  ;
}

#define SPRITE_DEFAULT 7

//int test_render_num;
void _TestObjectRenderF(Overlay *testObject, int sprite_num, eFlipDirection flip, const string test_name)
{
  Wait(1); // let engine redraw
  int sprite_w = Game.SpriteWidth[sprite_num];
  int sprite_h = Game.SpriteHeight[sprite_num];
  int sprite_x = testObject.X;
  int sprite_y = testObject.Y;
  //tap.Comment(String.Format("sprite_x = %d, sprite_y = %d, sprite_w = %d, sprite_h = %d", sprite_x, sprite_y, sprite_w, sprite_h));
  //SaveScreenShot(String.Format("_overlay_%d.bmp", test_render_num));
  //test_render_num++;
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(sprite_x, sprite_y, sprite_w, sprite_h, sprite_num, flip), test_name);
}

void _TestObjectRender(Overlay *testObject, int sprite_num, const string test_name)
{
  _TestObjectRenderF(testObject, sprite_num, eFlipNone, test_name);
}

void _TestObjectViewFrame(Overlay *testObject, int view, int loop, int frame, const string test_name)
{
  ViewFrame* vf = Game.GetViewFrame(view, loop, frame);
  _TestObjectRenderF(testObject, vf.Graphic, vf.Flipped, test_name);
}

void TestGraphicPosition(Overlay *testObject)
{
  testObject.Graphic = SPRITE_DEFAULT;
  _TestObjectRender(testObject, SPRITE_DEFAULT, "Overlay: graphic position");
}

void TestViewRender(AnimatedOverlay *testObject)
{
  // NOTE: add some animation delay, because our TestRender function also does Wait(1) before screenshot
  testObject.Animate(VFLIP, 0, 2, eOnce, eNoBlock, eForwards);
  while (testObject.Animating)
  {
    int frame = testObject.Frame;
    String test_name = String.Format("AnimatedOverlay: view frame %d, ", frame);
    switch (frame)
    {
    case 0:
    case 1:
      test_name = test_name.Append("normal");
      break;
    case 2:
    case 3:
      test_name = test_name.Append("h-flip");
      break;
    case 4:
    case 5:
      test_name = test_name.Append("v-flip");
      break;
    case 6:
    case 7:
      test_name = test_name.Append("hv-flip");
      break;
    }
    _TestObjectViewFrame(testObject, VFLIP, 0, frame, test_name);
    
    while (testObject.Frame == frame)
    {
      Wait(1);
    }
  }
}

void TestOverlays()
{
  tap.Comment("start Overlays tests");
  tap.Comment(String.Format("System.HardwareAcceleration = %d", System.HardwareAcceleration));
  
  // Disable characters and objects, avoid interfering with our tests
  TestHelpers.HideRoomObjects();
  
  Overlay* over = Overlay.CreateGraphical(100, 100, 0);
  TestGraphicPosition(over);
  over.Remove();
  
  AnimatedOverlay* aover = AnimatedOverlay.CreateAnimated(100, 100, 0);
  TestViewRender(aover);
  aover.Remove();
  
  tap.Comment("end Overlays tests");
}
