// Test GUI Properties Module Script
//
// This module tests that all GUI and GUI control properties
// are properly set and read.
//
int GetTestGUIPropertiesCount()
{
  return 0
    + 24      // gui properties
    + 15 * 6  // common gui control properties
#ifver 3.6.3
    + 9 * 6
#endif
    + 63      // gui control properties
#ifver 3.6.3
    + 10
#endif
    ;
}

#define NORMAL_GUI_ID 1
#define NORMAL_GUI_SCRIPTNAME "gGui2"
#define NORMAL_GUI_CONTROL_COUNT 6

#define BUTTON_ID 0
#define BUTTON_SCRIPT_NAME "Button2"
#define INVWINDOW_ID 5
#define INVWINDOW_SCRIPT_NAME "InventoryWindow1"
#define LABEL_ID 1
#define LABEL_SCRIPT_NAME "Label1"
#define LISTBOX_ID 3
#define LISTBOX_SCRIPT_NAME "ListBox1"
#define SLIDER_ID 4
#define SLIDER_SCRIPT_NAME "Slider1"
#define TEXTBOX_ID 2
#define TEXTBOX_SCRIPT_NAME "TextBox1"

void TestGUIControlProperties(GUIControl* gc, String gc_name, int gc_id, String gc_scriptname, GUI* parent_gui)
{
  gc.Clickable = false;
  tap.is_int(gc.Clickable, false, String.Format("%s.Clickable (false)", gc_name));
  gc.Clickable = true;
  tap.is_int(gc.Clickable, true, String.Format("%s.Clickable (true)", gc_name));
  gc.Enabled = false;
  tap.is_int(gc.Enabled, false, String.Format("%s.Enabled (false)", gc_name));
  gc.Enabled = true;
  tap.is_int(gc.Enabled, true, String.Format("%s.Enabled (true)", gc_name));
  gc.Height = 25;
  tap.is_int(gc.Height, 25, String.Format("%s.Height", gc_name));
  tap.is_int(gc.ID, gc_id, String.Format("%s.ID", gc_name));
  tap.is_int(gc.OwningGUI == parent_gui, true, String.Format("%s.OwningGUI", gc_name));
  tap.is(gc.ScriptName, gc_scriptname, String.Format("%s.ScriptName", gc_name));
  gc.Transparency = 40;
  tap.is_int(gc.Transparency, 40, String.Format("%s.Transparency", gc_name));
  gc.Visible = false;
  tap.is_int(gc.Visible, false, String.Format("%s.Visible (false)", gc_name));
  gc.Visible = true;
  tap.is_int(gc.Visible, true, String.Format("%s.Visible (true)", gc_name));
  gc.Width = 80;
  tap.is_int(gc.Width, 80, String.Format("%s.Width", gc_name));
  gc.X = 15;
  tap.is_int(gc.X, 15, String.Format("%s.X", gc_name));
  gc.Y = 24;
  tap.is_int(gc.Y, 24, String.Format("%s.Y", gc_name));
  gc.ZOrder = 4;
  tap.is_int(gc.ZOrder, 4, String.Format("%s.ZOrder", gc_name));
  
#ifver 3.6.3
  gc.BackgroundColor = 112233;
  tap.is_int(gc.BackgroundColor, 112233, String.Format("%s.BackgroundColor", gc_name));
  gc.BorderColor = 332211;
  tap.is_int(gc.BorderColor, 332211, String.Format("%s.BorderColor", gc_name));
  gc.BorderWidth = 3;
  tap.is_int(gc.BorderWidth, 3, String.Format("%s.BorderWidth", gc_name));
  gc.PaddingX = 5;
  tap.is_int(gc.PaddingX, 5, String.Format("%s.PaddingX", gc_name));
  gc.PaddingY = 6;
  tap.is_int(gc.PaddingY, 6, String.Format("%s.PaddingY", gc_name));
  gc.ShowBorder = false;
  tap.is_int(gc.ShowBorder, false, String.Format("%s.ShowBorder (false)", gc_name));
  gc.ShowBorder = true;
  tap.is_int(gc.ShowBorder, true, String.Format("%s.ShowBorder (true)", gc_name));
  gc.SolidBackground = false;
  tap.is_int(gc.SolidBackground, false, String.Format("%s.SolidBackground (false)", gc_name));
  gc.SolidBackground = true;
  tap.is_int(gc.SolidBackground, true, String.Format("%s.SolidBackground (true)", gc_name));
#endif
}

void TestGUIProperties()
{
  tap.Comment("start GUI Properties tests");
  
  {
    tap.Comment("Normal GUI Properties");
    
    GUI *g  = gGui2;
    tap.is_int(g.AsTextWindow == null, true, "GUI.TextWindow get (normal)");
    g.BackgroundColor = 10;
    tap.is_int(g.BackgroundColor, 10, "GUI.BackgroundColor");
    g.BackgroundGraphic = 5;
    tap.is_int(g.BackgroundGraphic, 5, "GUI.BackgroundGraphic");
    g.BorderColor = 112233;
    tap.is_int(g.BorderColor, 112233, "GUI.BorderColor");
    g.Clickable = false;
    tap.is_int(g.Clickable, false, "GUI.Clickable (false)");
    g.Clickable = true;
    tap.is_int(g.Clickable, true, "GUI.Clickable (true)");
    tap.is_int(g.ControlCount, NORMAL_GUI_CONTROL_COUNT, "GUI.ControlCount");
    g.Height = 100;
    tap.is_int(g.Height, 100, "GUI.Height");
    tap.is_int(g.ID, NORMAL_GUI_ID, "GUI.ID");
    //g.PopupStyle = eGUIPopupMouseYPos;
    //tap.is_int(g.PopupStyle, eGUIPopupMouseYPos, "GUI.PopupStyle");
    tap.is_int(g.PopupStyle, eGUIPopupNormal, "GUI.PopupStyle");
    g.PopupYPos = 22;
    tap.is_int(g.PopupYPos, 22, "GUI.PopupYPos");
    tap.is(g.ScriptName, NORMAL_GUI_SCRIPTNAME, "GUI.ScriptName");
    // TODO: cannot test dynamic PopupStyle change, because it's not settable
    /*
    g.PopupStyle = eGUIPopupNormal;
    g.Visible = true;
    tap.is_int(g.Shown, true, "GUI.Shown (1)");
    g.Visible = false;
    tap.is_int(g.Shown, false, "GUI.Shown (2)");
    g.PopupStyle = eGUIPopupMouseYPos;
    g.Visible = true;
    tap.is_int(g.Shown, false, "GUI.Shown (3)");
    */
    g.Visible = true;
    tap.is_int(g.Shown, true, "GUI.Shown (false)");
    g.Visible = false;
    tap.is_int(g.Shown, false, "GUI.Shown (true)");
    // TODO: test that legacy transparency range is inaccurate
    g.Transparency = 70;
    tap.is_int(g.Transparency, 70, "GUI.Transparency");
    g.Visible = false;
    tap.is_int(g.Visible, false, "GUI.Visible (false)");
    g.Visible = true;
    tap.is_int(g.Visible, true, "GUI.Visible (true)");
    g.Width = 220;
    tap.is_int(g.Width, 220, "GUI.Width");
    g.X = 60;
    tap.is_int(g.X, 60, "GUI.X");
    g.Y = 40;
    tap.is_int(g.Y, 40, "GUI.Y");
    g.ZOrder = -15;
    tap.is_int(g.ZOrder, -15, "GUI.ZOrder");
    
    
    tap.Comment("TextWindow GUI Properties");
    
    GUI *gtw = gTextWindow1;
    TextWindowGUI *tw = gtw.AsTextWindow;
    tap.is_int(gtw.AsTextWindow != null, true, "GUI.TextWindow get (textwindow)");
    tw.TextColor = 12;
    tap.is_int(tw.TextColor, 12, "GUI.TextColor");
    tw.TextPadding = 4;
    tap.is_int(tw.TextPadding, 4, "GUI.TextPadding");
  }
  
  {
    tap.Comment("Button Properties");
    
    Button* btn = Button2;
    
    TestGUIControlProperties(btn, "Button", BUTTON_ID, BUTTON_SCRIPT_NAME, gGui2);
    
    tap.is_int(btn.Animating, false, "Button.Animating (false)");
    btn.Animate(VWALKING2, 1, 0, eOnce, eNoBlock, eForwards, 4);
    tap.is_int(btn.Animating, true, "Button.Animating (true)");
    tap.is_int(btn.View, VWALKING2, "Button.View");
    tap.is_int(btn.Loop, 1, "Button.Loop");
    tap.is_int(btn.Frame, 4, "Button.Frame");
    tap.is_int(btn.Graphic, 0, "Button.Graphic (0)");
    btn.ClipImage = false;
    tap.is_int(btn.ClipImage, false, "Button.ClipImage (false)");
    btn.ClipImage = true;
    tap.is_int(btn.ClipImage, true, "Button.ClipImage (true)");
    btn.Font = eFontFont1;
    tap.is_int(btn.Font, eFontFont1, "Button.Font");
    btn.MouseOverGraphic = 1;
    tap.is_int(btn.MouseOverGraphic, 1, "Button.MouseOverGraphic");
    btn.NormalGraphic = 2;
    tap.is_int(btn.NormalGraphic, 2, "Button.NormalGraphic");
    tap.is_int(btn.Graphic, 2, "Button.Graphic (2)");
    btn.PushedGraphic = 3;
    tap.is_int(btn.PushedGraphic, 3, "Button.PushedGraphic");
    btn.Text = "This is a text";
    tap.is(btn.Text, "This is a text", "Button.Text");
    btn.TextAlignment = eAlignTopRight;
    tap.is_int(btn.TextAlignment, eAlignTopRight, "Button.TextAlignment");
    btn.TextColor = 65535;
    tap.is_int(btn.TextColor, 65535, "Button.TextColor");
    
#ifver 3.6.3
    btn.ColorStyle = eGUIButtonDynamicFlat;
    tap.is_int(btn.ColorStyle, eGUIButtonDynamicFlat, "Button.ColorStyle");
    btn.MouseOverBackgroundColor = 12810;
    tap.is_int(btn.MouseOverBackgroundColor, 12810, "Button.MouseOverBackgroundColor");
    btn.MouseOverBorderColor = 12820;
    tap.is_int(btn.MouseOverBorderColor, 12820, "Button.MouseOverBorderColor");
    btn.MouseOverTextColor = 12830;
    tap.is_int(btn.MouseOverTextColor, 12830, "Button.MouseOverTextColor");
    btn.PushedBackgroundColor = 12840;
    tap.is_int(btn.PushedBackgroundColor, 12840, "Button.PushedBackgroundColor");
    btn.PushedBorderColor = 12850;
    tap.is_int(btn.PushedBorderColor, 12850, "Button.PushedBorderColor");
    btn.PushedTextColor = 12860;
    tap.is_int(btn.PushedTextColor, 12860, "Button.PushedTextColor");
    btn.BorderShadeColor = 12870;
    tap.is_int(btn.BorderShadeColor, 12870, "Button.BorderShadeColor");
#endif
  }
  
  {
    tap.Comment("InventoryWindow Properties");
    
    InvWindow* inv = InventoryWindow1;
    
    TestGUIControlProperties(inv, "InvWindow", INVWINDOW_ID, INVWINDOW_SCRIPT_NAME, gGui2);
    
    inv.CharacterToUse = cInventoryHolder;
    tap.is_int(inv.CharacterToUse == cInventoryHolder, true, "InvWindow.CharacterToUse");
    
    // Following tests depend on the control's size
    inv.Height = 25;
    inv.Width = 80;
    inv.BorderWidth = 0;
    inv.PaddingX = 0;
    inv.PaddingY = 0;
    inv.ShowBorder = false;
    
    cInventoryHolder.AddInventory(iInvItemOne);
    cInventoryHolder.AddInventory(iInvItemTwo);
    cInventoryHolder.AddInventory(iInvItemThree);
    tap.is_int(inv.ItemAtIndex[0] == iInvItemOne, true, "InvWindow.ItemAtIndex (0)");
    tap.is_int(inv.ItemAtIndex[1] == iInvItemTwo, true, "InvWindow.ItemAtIndex (1)");
    tap.is_int(inv.ItemAtIndex[2] == iInvItemThree, true, "InvWindow.ItemAtIndex (2)");
    tap.is_int(inv.ItemCount, 3, "InvWindow.ItemCount");
    inv.ItemHeight = 5;
    tap.is_int(inv.ItemHeight, 5, "InvWindow.ItemHeight");
    inv.ItemWidth = 20;
    tap.is_int(inv.ItemWidth, 20, "InvWindow.ItemWidth");
    tap.is_int(inv.ItemsPerRow, 4, "InvWindow.ItemsPerRow");
    tap.is_int(inv.RowCount, 5, "InvWindow.RowCount");
    inv.TopItem = 1;
    tap.is_int(inv.TopItem, 1, "InvWindow.TopItem");
  }
  
  {
    tap.Comment("Label Properties");
    
    Label* lbl = Label1;
    
    TestGUIControlProperties(lbl, "Label", LABEL_ID, LABEL_SCRIPT_NAME, gGui2);
    
    lbl.Font = eFontFont1;
    tap.is_int(lbl.Font, eFontFont1, "Label.Font");
    lbl.Text = "This is a text";
    tap.is(lbl.Text, "This is a text", "Label.Text");
    lbl.TextAlignment = eAlignTopRight;
    tap.is_int(lbl.TextAlignment, eAlignTopRight, "Label.TextAlignment");
    lbl.TextColor = 65535;
    tap.is_int(lbl.TextColor, 65535, "Label.TextColor");
  }
  
  {
    tap.Comment("ListBox Properties");
    
    ListBox* box = ListBox1;
    
    TestGUIControlProperties(box, "ListBox", LISTBOX_ID, LISTBOX_SCRIPT_NAME, gGui2);
    
    // Following tests depend on the control's size
    box.Height = 25;
    box.Width = 80;
    box.ShowBorder = true;
    box.BorderWidth = 1;
    box.PaddingX = 0;
    box.PaddingY = 0;
    
    box.Font = eFontFont1;
    tap.is_int(box.Font, eFontFont1, "ListBox.Font");
    box.TextAlignment = eAlignTopRight;
    tap.is_int(box.TextAlignment, eAlignTopRight, "ListBox.TextAlignment");
    box.TextColor = 65535;
    tap.is_int(box.TextColor, 65535, "ListBox.TextColor");
    
    box.AddItem("item1");
    box.AddItem("item2");
    box.AddItem("item3");
    tap.is_int(box.ItemCount, 3, "ListBox.ItemCount");
    tap.is(box.Items[0], "item1", "ListBox.Items (0)");
    tap.is(box.Items[1], "item2", "ListBox.Items (1)");
    tap.is(box.Items[2], "item3", "ListBox.Items (2)");
    // NOTE: this is calculated from font height and listbox.Height
    tap.is_int(box.RowCount, 2, "ListBox.RowCount");
    // TODO: find a way to test SaveGameSlots
    //box.SaveGameSlots
    box.SelectedBackColor = 32767;
    tap.is_int(box.SelectedBackColor, 32767, "ListBox.SelectedBackColor");
    box.SelectedIndex = 2;
    tap.is_int(box.SelectedIndex, 2, "ListBox.SelectedIndex");
    box.SelectedTextColor = 16;
    tap.is_int(box.SelectedTextColor, 16, "ListBox.SelectedTextColor");
    box.ShowBorder = false;
    tap.is_int(box.ShowBorder, false, "ListBox.ShowBorder (false)");
    box.ShowBorder = true;
    tap.is_int(box.ShowBorder, true, "ListBox.ShowBorder (true)");
    box.ShowScrollArrows = false;
    tap.is_int(box.ShowScrollArrows, false, "ListBox.ShowScrollArrows (false)");
    box.ShowScrollArrows = true;
    tap.is_int(box.ShowScrollArrows, true, "ListBox.ShowScrollArrows (true)");
    box.TopItem = 1;
    tap.is_int(box.TopItem, 1, "ListBox.TopItem");
  }
  
  {
    tap.Comment("Slider Properties");
    
    Slider* sl = Slider1;
    
    TestGUIControlProperties(sl, "Slider", SLIDER_ID, SLIDER_SCRIPT_NAME, gGui2);
    
    sl.BackgroundGraphic = 5;
    tap.is_int(sl.BackgroundGraphic, 5, "Slider.BackgroundGraphic");
    sl.HandleGraphic = 6;
    tap.is_int(sl.HandleGraphic, 6, "Slider.HandleGraphic");
    sl.HandleOffset = 2;
    tap.is_int(sl.HandleOffset, 2, "Slider.HandleOffset");
    sl.Max = 100;
    tap.is_int(sl.Max, 100, "Slider.Max");
    sl.Min = -20;
    tap.is_int(sl.Min, -20, "Slider.Min");
    sl.Value = 44;
    tap.is_int(sl.Value, 44, "Slider.Value");
    
    // Min and Max range clamping the value
    sl.Min = 5;
    sl.Max = 10;
    sl.Value = 0;
    tap.is_int(sl.Value, 5, "Slider.Value clamped to min-max");
    // Allow Min = Max
    sl.Min = 10;
    tap.is_int(sl.Min, 10, "Slider.Min set to Max, assert Min");
    tap.is_int(sl.Max, 10, "Slider.Min set to Max, assert Min");
    tap.is_int(sl.Value, 10, "Slider.Min set to Max, assert Value");
    sl.Min = 5;
    sl.Max = 5;
    tap.is_int(sl.Min, 5, "Slider.Max set to Min, assert Min");
    tap.is_int(sl.Max, 5, "Slider.Max set to Min, assert Min");
    tap.is_int(sl.Value, 5, "Slider.Min set to Max, assert Value");
    
#ifver 3.6.3
    sl.HandleColor = 13000;
    tap.is_int(sl.HandleColor, 13000, "Slider.HandleColor");
    sl.BorderShadeColor = 13100;
    tap.is_int(sl.BorderShadeColor, 13100, "Slider.BorderShadeColor");
#endif
  }
  
  {
    tap.Comment("TextBox Properties");
    
    TextBox* tb = TextBox1;
    
    TestGUIControlProperties(tb, "TextBox", TEXTBOX_ID, TEXTBOX_SCRIPT_NAME, gGui2);
    
    tb.Font = eFontFont1;
    tap.is_int(tb.Font, eFontFont1, "TextBox.Font");
    tb.TextColor = 65535;
    tap.is_int(tb.TextColor, 65535, "TextBox.TextColor");
    
    tb.ShowBorder = false;
    tap.is_int(tb.ShowBorder, false, "TextBox.ShowBorder (false)");
    tb.ShowBorder = true;
    tap.is_int(tb.ShowBorder, true, "TextBox.ShowBorder (true)");
  }
  
  // CLEANUP
  gGui2.Visible = false;
  Button2.NormalGraphic = 0;
  
  tap.Comment("end GUI Properties tests");
}