// Test DrawingSurface Module Script
int GetTestDrawingSurfaceCount()
{
  return 16;
}

#define SPR_DRAW_CIRCLE         1
#define SPR_DRAW_LINE           2
#define SPR_DRAW_RECTANGLE      3
#define SPR_DRAW_TRIANGLE       4

#define COLOR_TEAL              10922
#define COLOR_TEAL_LIGHT        21845
#define COLOR_RED_LIGHT         43691
#define COLOR_BLUE_DARK         10606
#define COLOR_GREEN_LIGHT       18126
#define COLOR_WHITE             65535
#define COLOR_MAROON            16420
#define COLOR_YELLOW_DARK       54564
#define COLOR_PURPLE            33405


bool _IsSpriteEqual(int graphic_1, int graphic_2)
{
  if(graphic_1 == graphic_2) return true;
  
  DynamicSprite* dynspr_1 = DynamicSprite.CreateFromExistingSprite(graphic_1);
  DynamicSprite* dynspr_2 = DynamicSprite.CreateFromExistingSprite(graphic_2);
  DrawingSurface* surf_1 = dynspr_1.GetDrawingSurface();
  DrawingSurface* surf_2 = dynspr_2.GetDrawingSurface();
  
  if(surf_1.Width != surf_2.Width || surf_1.Height != surf_2.Height) return false;
  
  
  for(int i=0; i<surf_1.Width; i++) {
    for(int j=0; j<surf_1.Height; j++) {
      if(surf_1.GetPixel(i, j) != surf_2.GetPixel(i, j)) return false;
    }
  }
  
  return true;
}

DynamicSprite* _CreateSpriteAlpha(int w, int h)
{
  return DynamicSprite.Create(w, h, true);
}

void TestDrawingSurface()
{  
  tap.Comment("start DrawingSurface tests");
  
  // DrawingSurface.GetPixel, DrawingSurface.Clear, DrawingSurface.DrawPixel
  {
    DynamicSprite* dynspr = _CreateSpriteAlpha(16, 16);
    DrawingSurface* surface = dynspr.GetDrawingSurface();
    
    surface.Clear(COLOR_TEAL);
    tap.is_int(surface.GetPixel(0, 0), COLOR_TEAL, "DrawingSurface.Clear sets the color of the surface (1)");
    tap.is_int(surface.GetPixel(5, 5), COLOR_TEAL, "DrawingSurface.Clear sets the color of the surface (2)");
    tap.is_int(surface.GetPixel(15, 15), COLOR_TEAL, "DrawingSurface.Clear sets the color of the surface (3)");
    surface.DrawingColor = COLOR_TEAL_LIGHT;
    surface.DrawPixel(1, 1);
    surface.DrawPixel(7, 2);
    tap.is_int(surface.GetPixel(0, 0), COLOR_TEAL, "DrawingSurface.DrawPixel works (1)");
    tap.is_int(surface.GetPixel(1, 1), COLOR_TEAL_LIGHT, "DrawingSurface.DrawPixel works (2)");
    tap.is_int(surface.GetPixel(2, 1), COLOR_TEAL, "DrawingSurface.DrawPixel works (3)");
    tap.is_int(surface.GetPixel(2, 2), COLOR_TEAL, "DrawingSurface.DrawPixel works (4)");
    tap.is_int(surface.GetPixel(7, 2), COLOR_TEAL_LIGHT, "DrawingSurface.DrawPixel works (5)");
    tap.is_int(surface.GetPixel(7, 1), COLOR_TEAL, "DrawingSurface.DrawPixel works (6)");
    tap.is_int(surface.GetPixel(7, 3), COLOR_TEAL, "DrawingSurface.DrawPixel works (7)");
    tap.is_int(surface.GetPixel(6, 2), COLOR_TEAL, "DrawingSurface.DrawPixel works (8)");
    tap.is_int(surface.GetPixel(8, 2), COLOR_TEAL, "DrawingSurface.DrawPixel works (9)");
    surface.Release();
    dynspr.Delete();
  }
  
  
  // DrawingSurface.DrawCircle
  {
    DynamicSprite* dynspr = _CreateSpriteAlpha(32, 16);
    DrawingSurface* surface = dynspr.GetDrawingSurface();
    
    surface.Clear(COLOR_RED_LIGHT);
    surface.DrawingColor = COLOR_BLUE_DARK;
    surface.DrawCircle(5, 8, 5);
    surface.DrawCircle(14, 8, 6);
    surface.DrawCircle(20, 9, 10);
    
    surface.Release();
    
    tap.ok(_IsSpriteEqual(dynspr.Graphic, SPR_DRAW_CIRCLE), "DrawingSurface.DrawCircle works");
    
    dynspr.Delete();    
  }

  // DrawingSurface.DrawLine
  {
    DynamicSprite* dynspr = _CreateSpriteAlpha(32, 16);
    DrawingSurface* surface = dynspr.GetDrawingSurface();
    
    surface.Clear(COLOR_RED_LIGHT);
    surface.DrawingColor = COLOR_BLUE_DARK;
    surface.DrawLine(0, 0, 8, 8, 3);
    surface.DrawLine(5, 1, 15, 11);
    surface.DrawLine(8, 8, 31, 8);
    surface.DrawLine(16, 0, 16, 15);
    surface.DrawingColor = COLOR_GREEN_LIGHT;
    surface.DrawLine(5, 7, 3, 3);
    surface.DrawLine(25, 13, 9, 14);
    surface.DrawingColor = COLOR_WHITE;
    surface.DrawLine(14, 0, 14, 1);

    surface.Release();

    tap.ok(_IsSpriteEqual(dynspr.Graphic, SPR_DRAW_LINE), "DrawingSurface.DrawLine works");

    dynspr.Delete();    
  }

  // DrawingSurface.DrawRectangle
  {
    DynamicSprite* dynspr = _CreateSpriteAlpha(32, 16);
    DrawingSurface* surface = dynspr.GetDrawingSurface();
    
    surface.Clear(COLOR_MAROON);
    surface.DrawingColor = COLOR_YELLOW_DARK;
    surface.DrawRectangle(5, 5, 12, 6);
    surface.DrawRectangle(8, 8, 31, 14);
    surface.DrawingColor = COLOR_PURPLE;
    surface.DrawRectangle(11, 13, 11, 18);
    surface.DrawRectangle(15, 6, 25, 15);

    surface.Release();

    tap.ok(_IsSpriteEqual(dynspr.Graphic, SPR_DRAW_RECTANGLE), "DrawingSurface.DrawRectangle works");
    
    dynspr.Delete();    
  }

  // DrawingSurface.DrawTriangle
  {
    DynamicSprite* dynspr = _CreateSpriteAlpha(32, 16);
    DrawingSurface* surface = dynspr.GetDrawingSurface();
    
    surface.Clear(COLOR_RED_LIGHT);
    surface.DrawingColor = COLOR_BLUE_DARK;
    surface.DrawTriangle(1, 2, 4, 5, 3, 9);
    surface.DrawTriangle(23, 10, 31, 15, 20, 6);
    surface.DrawingColor = COLOR_GREEN_LIGHT;
    surface.DrawTriangle(5, 6, 8, 15, 0, 16);
    
    surface.Release();
    
    tap.ok(_IsSpriteEqual(dynspr.Graphic, SPR_DRAW_TRIANGLE), "DrawingSurface.DrawTriangle works");

    dynspr.Delete();    
  }
  
  tap.Comment("end DrawingSurface tests");
}