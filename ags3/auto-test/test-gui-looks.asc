// Test GUI Looks Module Script
#define DELAYED_GUI_BUTTON_TEST_CASES         1
#define DELAYED_GUI_BUTTON_TEST_COLORED_CASES 2
int GetTestGUILooksCount()
{
  return 15
  + 8 // Colored buttons in 3.6.3
  // Delayed tests
  + DELAYED_GUI_BUTTON_TEST_CASES
  + DELAYED_GUI_BUTTON_TEST_COLORED_CASES
  ;
}

#define BUTTON_SPRITE_NORMAL              25
#define BUTTON_SPRITE_MOUSEOVER           29
#define SLIDER_BG_SPRITE                  27
#define SLIDER_HANDLE_SPRITE              28

#define BUTTON_SHOT_TOPLEFTALIGN          11
#define BUTTON_SHOT_CENTERALIGN           12
#define BUTTON_SHOT_BOTTOMRIGHTALIGN      13
#define BUTTON_SHOT_GRAPHICNOTEXT         14
#define BUTTON_SHOT_GRAPHICWITHTEXT       15
#define BUTTON_SHOT_GRAPHICMOUSEOVER      30

#define BUTTON_SHOT_COLOREDDEFAULT        33
#define BUTTON_SHOT_COLOREDNOBORDER       34
#define BUTTON_SHOT_COLOREDNOBG           35
#define BUTTON_SHOT_COLOREDFLAT           36
#define BUTTON_SHOT_COLOREDDEFMOUSEOVER   37
#define BUTTON_SHOT_COLOREDFLATMOUSEOVER  38

#define LABEL_SHOT_LEFTALIGN              16
#define LABEL_SHOT_CENTERALIGN            17
#define LABEL_SHOT_RIGHTALIGN             18
#define LABEL_SHOT_MULTILINE              19
#define LABEL_SHOT_COLORED                39

#define LISTBOX_SHOT_NOBORDER             20
#define LISTBOX_SHOT_SHOWBORDER           21
#define LISTBOX_SHOT_COLORED              40

#define TEXTBOX_SHOT_STANDARD             26
#define TEXTBOX_SHOT_COLORED              41

#define SLIDER_SHOT_THIN                  22
#define SLIDER_SHOT_THICK                 23
#define SLIDER_SHOT_GRAPHICS              24
#define SLIDER_SHOT_COLORED               42

void PrepareParentGUI()
{
  TestHelpers.HideGUIs();
  
  gGui2.Visible = true;
  gGui2.X = 0;
  gGui2.Y = 0;
  gGui2.Width = Screen.Width;
  gGui2.Height = Screen.Height;
  gGui2.BackgroundGraphic = 0;
  gGui2.BackgroundColor = 2113; // 8, 8, 8
  gGui2.BorderColor = 0;
  gGui2.Transparency = 0;
  SetGameOption(OPT_WHENGUIDISABLED, 2);
  SetGameOption(OPT_CLIPGUICONTROLS, 1);
}

void PrepareDefaultButton(Button* btn)
{
  btn.Visible = true;
  btn.Enabled = true;
  btn.Clickable = true;
  btn.Transparency = 0;
  btn.Font = eFontFont0;
  btn.X = 20;
  btn.Y = 20;
  btn.Width = 80;
  btn.Height = 20;
  btn.Text = "New Button";
  btn.ColorStyle = eGUIButtonDefault;
  btn.ShowBorder = true;
  btn.SolidBackground = true;
  btn.BorderWidth = 1;
  btn.PaddingX = 1;
  btn.PaddingY = 1;
  btn.BackgroundColor = 7;
  btn.BorderColor = 15;
  btn.BorderShadeColor = 8;
  btn.NormalGraphic = 0;
  btn.MouseOverGraphic = 0;
  btn.PushedGraphic = 0;
}

void PrepareColoredDefaultButton(Button* btn)
{
  btn.Visible = true;
  btn.Enabled = true;
  btn.Clickable = true;
  btn.Transparency = 0;
  btn.Font = eFontFont0;
  btn.X = 20;
  btn.Y = 20;
  btn.Width = 80;
  btn.Height = 20;
  btn.Text = "New Button";
  btn.TextAlignment = eAlignTopLeft;  
  btn.ColorStyle = eGUIButtonDynamic;
  btn.ShowBorder = true;
  btn.SolidBackground = true;
  btn.BackgroundColor = 17213;
  btn.BorderColor = 18153;
  btn.BorderWidth = 2;
  btn.MouseOverBackgroundColor = 26706;
  btn.MouseOverTextColor = 62668;
  btn.PaddingX = 2;
  btn.PaddingY = 4;
  btn.BorderShadeColor = 5123;
  btn.TextColor = 65520;
  btn.NormalGraphic = 0;
  btn.MouseOverGraphic = 0;
  btn.PushedGraphic = 0;
}

void PrepareColoredFlatButton(Button* btn)
{
  btn.Visible = true;
  btn.Enabled = true;
  btn.Clickable = true;
  btn.Transparency = 0;
  btn.Font = eFontFont0;
  btn.X = 20;
  btn.Y = 20;
  btn.Width = 80;
  btn.Height = 20;
  btn.Text = "New Button";
  btn.TextAlignment = eAlignBottomRight;
  btn.ColorStyle = eGUIButtonDynamicFlat;
  btn.ShowBorder = true;
  btn.SolidBackground = true;
  btn.BackgroundColor = 17213;
  btn.BorderColor = 18153;
  btn.BorderWidth = 2;
  btn.MouseOverBackgroundColor = 18153;
  btn.MouseOverBorderColor = 17213;
  btn.MouseOverTextColor = 62668;
  btn.PaddingX = 0;
  btn.PaddingY = 0;
  btn.BorderShadeColor = 5123;
  btn.TextColor = 65520;
  btn.NormalGraphic = 0;
  btn.MouseOverGraphic = 0;
  btn.PushedGraphic = 0;
}

void TestGUILooks()
{
  tap.Comment("start GUI Looks tests");
  
  PrepareParentGUI();
  
  // BUTTON LOOKS
  {
    TestHelpers.HideGUIControls(gGui2);
    
    Button* btn = Button2;
    PrepareDefaultButton(btn);
    
    btn.Text = "New Button";
    btn.Width = 80;
    btn.Height = 20;
    btn.TextColor = 0;
    btn.TextAlignment = eAlignTopLeft;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_TOPLEFTALIGN), 
      "Button looks: Default, Text align Top-Left");

    btn.TextAlignment = eAlignMiddleCenter;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_CENTERALIGN), 
      "Button looks: Default, Text align Center");

    // NOTE: A range of versions between ? to 3.6.1 had a mistake where
    // Bottom and Right alignment caused gui text to be positioned
    // 1 extra pixel away from the bottom-right edge
    btn.TextAlignment = eAlignBottomRight;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_BOTTOMRIGHTALIGN), 
      "Button looks: Default, Text align Bottom-Right");
      
    btn.Width = Game.SpriteWidth[BUTTON_SPRITE_NORMAL];
    btn.Height = Game.SpriteHeight[BUTTON_SPRITE_NORMAL];
    btn.NormalGraphic = BUTTON_SPRITE_NORMAL;
    btn.Text = "";
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_GRAPHICNOTEXT), 
      "Button looks: Graphic no text");
      
    btn.NormalGraphic = BUTTON_SPRITE_NORMAL;
    btn.Text = "Text";
    btn.TextColor = 65520;
    btn.TextAlignment = eAlignMiddleCenter;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_GRAPHICWITHTEXT), 
      "Button looks: Graphic with text");
  }
  
  // BUTTON LOOKS [COLORED v3.6.3]
  {
    TestHelpers.HideGUIControls(gGui2);
    
    Button* btn = Button2;
    PrepareColoredDefaultButton(btn);
    
    btn.Text = "New Button";
    btn.TextAlignment = eAlignTopLeft;
    btn.Width = 80;
    btn.Height = 20;
    
    btn.ColorStyle = eGUIButtonDynamic;
    btn.ShowBorder = true;
    btn.SolidBackground = true;
    btn.BackgroundColor = 17213;
    btn.BorderColor = 18153;
    btn.BorderWidth = 2;
    btn.MouseOverBackgroundColor = 26706;
    btn.MouseOverTextColor = 62668;
    btn.PaddingX = 2;
    btn.PaddingY = 4;
    btn.BorderShadeColor = 5123;
    btn.TextColor = 65520;
    
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_COLOREDDEFAULT), 
      "Button looks: Colored button (default)");
    
    btn.ShowBorder = false;
    btn.SolidBackground = true;
    
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_COLOREDNOBORDER), 
      "Button looks: Colored button (no border)");
    
    btn.ShowBorder = true;
    btn.SolidBackground = false;
    
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_COLOREDNOBG), 
      "Button looks: Colored button (no background)");
    
    PrepareColoredFlatButton(btn);
    
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_COLOREDFLAT), 
      "Button looks: Colored button (flat)");
  }
  
  // LABEL LOOKS
  {
    TestHelpers.HideGUIControls(gGui2);
    
    Label* lbl = Label1;
    lbl.Visible = true;
    lbl.Enabled = true;
    lbl.Transparency = 0;
    lbl.Font = eFontFont0;
    lbl.X = 20;
    lbl.Y = 20;
    lbl.Width = 100;
    lbl.Height = 10;
    lbl.TextColor = 65504;
    lbl.ShowBorder = false;
    lbl.SolidBackground = false;
    lbl.PaddingX = 0;
    lbl.PaddingY = 0;
    
    lbl.Text = "New Label";
    lbl.TextAlignment = eAlignTopLeft;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(lbl.X, lbl.Y, lbl.Width, lbl.Height, LABEL_SHOT_LEFTALIGN), 
      "Label looks: Single line, Text align Top-Left");

    lbl.TextAlignment = eAlignTopCenter;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(lbl.X, lbl.Y, lbl.Width, lbl.Height, LABEL_SHOT_CENTERALIGN), 
      "Label looks: Single line, Text align Top-Center");
    
    // NOTE: A range of versions between ? to 3.6.1 had a mistake where
    // Bottom and Right alignment caused gui text to be positioned
    // 1 extra pixel away from the bottom-right edge
    lbl.TextAlignment = eAlignTopRight;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(lbl.X, lbl.Y, lbl.Width, lbl.Height, LABEL_SHOT_RIGHTALIGN), 
      "Label looks: Single line, Text align Top-Right");

    lbl.Text = "New Label\nNew Label\nNew Label";
    lbl.TextAlignment = eAlignTopLeft;
    lbl.Width = 100;
    lbl.Height = 32;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(lbl.X, lbl.Y, lbl.Width, lbl.Height, LABEL_SHOT_MULTILINE), 
      "Label looks: Multi line, Text align Top-Left");
  }
  
  // LABEL LOOKS [COLORED v3.6.3]
  {
    Label* lbl = Label1;
    
    lbl.BackgroundColor = 17213;
    lbl.BorderColor = 18153;
    lbl.BorderWidth = 1;
    lbl.Height = 12;
    lbl.PaddingX = 1;
    lbl.PaddingY = 1;
    lbl.ShowBorder = true;
    lbl.SolidBackground = true;
    lbl.Text = "New Label";
    lbl.TextAlignment = eAlignTopLeft;
    lbl.TextColor = 65504;
    lbl.Width = 100;
                
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(lbl.X, lbl.Y, lbl.Width, lbl.Height, LABEL_SHOT_COLORED), 
      "Label looks: colored");
  }
  
  // LISTBOX LOOKS
  {
    TestHelpers.HideGUIControls(gGui2);
    
    ListBox* box = ListBox1;
    box.Visible = true;
    box.Enabled = true;
    box.Transparency = 0;
    box.Font = eFontFont0;
    box.X = 20;
    box.Y = 20;
    box.Width = 120;
    box.Height = 80;
    box.TextColor = 65504;
    box.SelectedTextColor = 63;
    box.SelectedBackColor = 34815;
    box.ShowBorder = true;
    box.SolidBackground = false;
    box.BorderWidth = 1;
    box.BorderColor = 65504;
    box.PaddingX = 0;
    box.PaddingY = 0;
    box.Clear();
    for (int i = 0; i < 10; i++)
    {
        box.AddItem(String.Format("Item %d", i));
    }
    box.TopItem = 0;
    box.SelectedIndex = 0;
    
    box.ShowScrollArrows = false;
    box.TextAlignment = eAlignLeft;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(box.X, box.Y, box.Width, box.Height, LISTBOX_SHOT_NOBORDER), 
      "ListBox looks: No Arrows");
    
    box.ShowScrollArrows = true;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(box.X, box.Y, box.Width, box.Height, LISTBOX_SHOT_SHOWBORDER), 
      "ListBox looks: Show Arrows");
  }
  
  // LISTBOX LOOKS [COLORED v3.6.3]
  {
    ListBox* box = ListBox1;
    
    box.Width = 120;
    box.Height = 80;
    box.BackgroundColor = 17213;
    box.BorderColor = 18153;
    box.BorderWidth = 2;
    box.PaddingX = 1;
    box.PaddingY = 1;
    box.SelectedBackColor = 34815;
    box.SelectedTextColor = 63;
    box.ShowBorder = true;
    box.ShowScrollArrows = true;
    box.SolidBackground = true;
    box.TextAlignment = eAlignLeft;
    box.TextColor = 65504;
    
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(box.X, box.Y, box.Width, box.Height, LISTBOX_SHOT_COLORED), 
      "ListBox looks: colored");
  }
  
  // TEXTBOX LOOKS
  {
    TestHelpers.HideGUIControls(gGui2);
    
    TextBox* box = TextBox1;
    box.Visible = true;
    box.Enabled = true;
    box.Transparency = 0;
    box.Font = eFontFont0;
    box.X = 20;
    box.Y = 20;
    box.Width = 140;
    box.Height = 14;
    box.TextColor = 65504;
    box.ShowBorder = true;
    box.SolidBackground = false;
    box.BorderWidth = 1;
    box.BorderColor = 65504;
    box.PaddingX = 1;
    box.PaddingY = 1;
    box.Text = "Text Box Example";
    
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(box.X, box.Y, box.Width, box.Height, TEXTBOX_SHOT_STANDARD), 
      "TextBox looks: standard");
  }
  
  // TEXTBOX LOOKS [COLORED v3.6.3]
  {
    TextBox* box = TextBox1;
    box.BackgroundColor = 17213;
    box.BorderColor = 18153;
    box.BorderWidth = 2;
    box.Height = 16;
    box.PaddingX = 2;
    box.PaddingY = 2;
    box.ShowBorder = true;
    box.SolidBackground = true;
    box.TextColor = 65504;
    box.Width = 140;
    box.Text = "Text Box Example";
    
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(box.X, box.Y, box.Width, box.Height, TEXTBOX_SHOT_COLORED), 
      "TextBox looks: colored");
  }
  
  // SLIDER LOOKS
  {
    TestHelpers.HideGUIControls(gGui2);
    
    gGui2.BackgroundColor = 59164;
    
    Slider* sld = Slider1;
    sld.Visible = true;
    sld.Enabled = true;
    sld.Transparency = 0;
    sld.X = 20;
    sld.Y = 20;
    sld.Min = 0;
    sld.Max = 10;
    sld.Value = 0;
    sld.BackgroundGraphic = 0;
    sld.HandleGraphic = 0;
    sld.HandleOffset = 0;
    sld.BackgroundColor = 16;
    sld.BorderColor = 15;
    sld.HandleColor = 7;
    sld.BorderShadeColor = 8;
    
    sld.Width = 40;
    sld.Height = 5;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(sld.X, sld.Y, sld.Width, sld.Height, SLIDER_SHOT_THIN), 
      "Slider looks: thin");
      
    sld.Width = 40;
    sld.Height = 12;
    // NOTE: sliders are weird, their image can extend past the control bounds
    int offx1 = 0;
    int offx2 = 0;
    int offy1 = -1;
    int offy2 = 3;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(sld.X + offx1, sld.Y + offy1, sld.Width + (-offx1 + offx2), sld.Height + (-offy1 + offy2), SLIDER_SHOT_THICK), 
      "Slider looks: thick");
      
    sld.Width = 100;
    sld.Height = 10;
    sld.BackgroundGraphic = SLIDER_BG_SPRITE;
    sld.HandleGraphic = SLIDER_HANDLE_SPRITE;
    sld.HandleOffset = -4;
    // NOTE: sliders are weird, their image can extend past the control bounds
    offx1 = -2;
    offx2 = 0;
    offy1 = -3;
    offy2 = 0;
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(sld.X + offx1, sld.Y + offy1, sld.Width + (-offx1 + offx2), sld.Height + (-offy1 + offy2), SLIDER_SHOT_GRAPHICS), 
      "Slider looks: graphics");
  }
  
  // SLIDER LOOKS [COLORED v3.6.3]
  {
    Slider* sld = Slider1;
    
    sld.Height = 5;
    sld.Width = 40;
    sld.BackgroundColor = 43275;
    sld.BackgroundGraphic = 0;
    sld.BorderColor = 64974;
    sld.BorderWidth = 1;
    sld.HandleColor = 61068;
    sld.HandleGraphic = 0;
    sld.HandleOffset = 0;
    sld.PaddingX = 0;
    sld.PaddingY = 0;
    sld.BorderShadeColor = 18628;
    sld.ShowBorder = true;
    sld.SolidBackground = true;
    
    Wait(1);
    tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(sld.X, sld.Y, sld.Width, sld.Height, SLIDER_SHOT_COLORED), 
      "Slider looks: colored");
  }
  
  // CLEANUP
  gGui2.Visible = false;
  
  tap.Comment("end GUI Looks tests");
}

void DoGUILooksDelayedTests(int counter)
{
  if (counter == 0)
  {
    PrepareParentGUI();
  }
  
  // BUTTON LOOKS
  // WARNING: this test requires window to stay in focus, 
  // because otherwise the mouse position breaks. This problem could be resolved, 
  // if engine supported emulated cursor position, not directly connected to mouse device.
  {
    Button* btn = Button2;
    // Each case has 2 counter increments: test run + results check
    if (counter == 0)
    {
      TestHelpers.HideGUIControls(gGui2);
      PrepareDefaultButton(btn);
      
      btn.Width = Game.SpriteWidth[BUTTON_SPRITE_NORMAL];
      btn.Height = Game.SpriteHeight[BUTTON_SPRITE_NORMAL];
      btn.NormalGraphic = BUTTON_SPRITE_NORMAL;
      btn.MouseOverGraphic = BUTTON_SPRITE_MOUSEOVER;
      btn.Text = "";
      
      TestHelpers.LockCursorAt(gGui2.X + btn.X + btn.Width / 2, gGui2.Y + btn.Y + btn.Height / 2);
    }
    else if (counter == 1)
    {
      tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_GRAPHICMOUSEOVER), 
        "Button looks: MouseOver graphic");
    }
    else if (counter == 2)
    {
      PrepareColoredDefaultButton(btn);
      TestHelpers.LockCursorAt(gGui2.X + btn.X + btn.Width / 2, gGui2.Y + btn.Y + btn.Height / 2);
    }
    else if (counter == 3)
    {
      tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_COLOREDDEFMOUSEOVER), 
        "Button looks: MouseOver default colored");
    }
    else if (counter == 4)
    {
      PrepareColoredFlatButton(btn);
      TestHelpers.LockCursorAt(gGui2.X + btn.X + btn.Width / 2, gGui2.Y + btn.Y + btn.Height / 2);
    }
    else if (counter == 5)
    {
      tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(btn.X, btn.Y, btn.Width, btn.Height, BUTTON_SHOT_COLOREDFLATMOUSEOVER), 
        "Button looks: MouseOver flat colored");
    }
  }
}

int TestGUILooksDelayed(int counter)
{
  if (counter == 0)
  {
    tap.Comment("start GUI Looks [delayed] tests");
    SetGameSpeed(1000);
  }
  
  DoGUILooksDelayedTests(counter);
  
  // Each test case has a prepare/run and test steps, so counter number is doubled
  int total_counter = DELAYED_GUI_BUTTON_TEST_CASES * 2
    + DELAYED_GUI_BUTTON_TEST_COLORED_CASES * 2;
  
  counter++;
  if (counter < total_counter)
  {
    return counter;
  }
  else
  {
    tap.Comment("end GUI Looks [delayed] tests");
    return -1;
  }
}
