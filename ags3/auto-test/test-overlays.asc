// Test Overlays Module Script

int GetTestOverlaysCount()
{
  return 0
  + 4 * 4   // overlay creation tests (x types)
  + 1 * 4   // 3.6.3 overlay creation tests (x types)
  + 6 * 2   // basic properties (x types)
  + 6 * 2   // 3.6.2 basic properties (x types)
  + 3 * 2   // 3.6.3 basic properties (x types)
  + 3       // graphic properties
  + 4       // old api tests
  + 1       // graphic pos
  ;
}

#define SPRITE_DEFAULT 9

#define OVERLAY_SHOT_TEXTUAL_1 31
#define OVERLAY_SHOT_TEXTUAL_2 32

void _TestOverlayCreation(Overlay* over, bool in_room, bool is_graphical, String over_name)
{
  tap.is_int(over.Valid, true, String.Format("Overlay.Create(%s): is valid", over_name));
  if (in_room)
    tap.is_int(over.InRoom, true, String.Format("Overlay.Create(%s): is room overlay", over_name));
  else
    tap.is_int(over.InRoom, false, String.Format("Overlay.Create(%s): is screen overlay", over_name));
  tap.is_int(over.Graphic > 0, true, String.Format("Overlay.Create(%s): has graphic", over_name));
  // TODO: this cannot be tested, as textual overlays also generate a sprite and set Graphic property;
  // figure out how to do this test?
  //if (is_graphical)
  //  tap.is_int(over.Graphic > 0, true, String.Format("Overlay.Create(%s): is graphical overlay", over_name));
  //else
  //  tap.is_int(over.Graphic == 0, true, String.Format("Overlay.Create(%s): is non-graphical overlay", over_name));
#ifver 3.6.3
  tap.is_int(over.Visible, true, String.Format("Overlay.Create(%s): is visible", over_name));
#endif
}

void _TestOverlayRemoval(Overlay* over, String over_name)
{
  over.Remove();
  tap.is_int(over.Valid, false, String.Format("Overlay.Remove(%s): is removed", over_name));
}

void _TestOverlayCreationAndRemoval(Overlay* over, bool in_room, bool is_graphical, String over_name)
{
  _TestOverlayCreation(over, in_room, is_graphical, over_name);
  _TestOverlayRemoval(over, over_name);
}

void TestOverlayCreation()
{
  _TestOverlayCreationAndRemoval(Overlay.CreateGraphical(100, 100, SPRITE_DEFAULT), false, true, "graphical screen");
  _TestOverlayCreationAndRemoval(Overlay.CreateRoomGraphical(100, 100, SPRITE_DEFAULT), true, true, "graphical room");
  _TestOverlayCreationAndRemoval(Overlay.CreateTextual(100, 100, 100, eFontFont0, 0, "x"), false, false, "textual screen");
  _TestOverlayCreationAndRemoval(Overlay.CreateRoomTextual(100, 100, 100, eFontFont0, 0, "x"), true, false, "textual room");
}

void TestOverlayBaseProperties(Overlay* over, String createdWithText, String over_name)
{
  over.Height = 144;
  tap.is_int(over.Height, 144, String.Format("%s.Height", over_name));
  over.Transparency = 50;
  tap.is_int(over.Transparency, 50, String.Format("%s.Transparency", over_name));
#ifver 3.6.3
  over.Visible = false;
  tap.is_int(over.Visible, false, String.Format("%s.Visible", over_name));
#endif
  over.Width = 156;
  tap.is_int(over.Width, 156, String.Format("%s.Width", over_name));
  over.X = 77;
  tap.is_int(over.X, 77, String.Format("%s.X", over_name));
  over.Y = 99;
  tap.is_int(over.Y, 99, String.Format("%s.Y", over_name));
  over.ZOrder = 111;
  tap.is_int(over.ZOrder, 111, String.Format("%s.ZOrder", over_name));
  
#ifver 3.6.2
  // Functions
  over.SetPosition(1, 2, 40, 50);
  tap.is_int(over.X, 1, String.Format("%s.SetPosition(): x", over_name));
  tap.is_int(over.Y, 2, String.Format("%s.SetPosition(): y", over_name));
  tap.is_int(over.Width, 40, String.Format("%s.SetPosition(): width", over_name));
  tap.is_int(over.Height, 50, String.Format("%s.SetPosition(): height", over_name));
  
  over.SetSize(60, 70);
  tap.is_int(over.Width, 60, String.Format("%s.SetSize(): width", over_name));
  tap.is_int(over.Height, 70, String.Format("%s.SetSize(): height", over_name));
#endif
#ifver 3.6.3
  tap.is(over.Text, createdWithText, String.Format("%s.Text: initial", over_name));
  over.Text = "custom";
  tap.is(over.Text, "custom", String.Format("%s.Text: set", over_name));
#endif
}

void TestOverlayGraphicProperties(Overlay* over, String over_name)
{
  over.Graphic = 4;
  tap.is_int(over.Graphic, 4, String.Format("%s.Graphic", over_name));
  tap.is_int(over.GraphicHeight, Game.SpriteHeight[4], String.Format("%s.GraphicHeight", over_name));
  tap.is_int(over.GraphicWidth, Game.SpriteWidth[4], String.Format("%s.GraphicWidth", over_name));
}

void TestGraphicOverlay()
{
  Overlay* over = Overlay.CreateGraphical(100, 100, 0);
  
  TestOverlayBaseProperties(over, "", "Graphic Overlay");
  TestOverlayGraphicProperties(over, "Graphic Overlay");
}

void TestTextualOverlay()
{
  Overlay* over = Overlay.CreateTextual(100, 100, 100, eFontFont0, 12345, "some string to display");

  TestOverlayBaseProperties(over, "some string to display", "Textual Overlay");
}

void TestOldOverlayAPI()
{
  int over = CreateTextOverlay(100, 100, 100, eFontFont0, 12345, "some string to display");
  tap.ok(over != 0, "CreateTextOverlay");
  
  // Old Overlay API did not have means to get the overlay position!
  // so we have to rely on screenshot match
  Wait(1);
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(100, 100, 100, 24, OVERLAY_SHOT_TEXTUAL_1), "CreateTextOverlay (position)");
  
  MoveOverlay(over, 120, 120);
  Wait(1);
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(120, 120, 100, 24, OVERLAY_SHOT_TEXTUAL_1), "MoveOverlay (position)");
  
  SetTextOverlay(over, 80, 80, 100, eFontFont0, 54321, "another string to display");
  Wait(1);
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(80, 80, 100, 24, OVERLAY_SHOT_TEXTUAL_2), "SetTextOverlay (position)");
  
  RemoveOverlay(over);
}

//int test_render_num;
void _TestObjectRenderF(Overlay *testObject, int sprite_num, eFlipDirection flip, String test_name)
{
  Wait(1); // let engine redraw
  int sprite_w = Game.SpriteWidth[sprite_num];
  int sprite_h = Game.SpriteHeight[sprite_num];
  int sprite_x = testObject.X;
  int sprite_y = testObject.Y;
  //tap.Comment(String.Format("sprite_x = %d, sprite_y = %d, sprite_w = %d, sprite_h = %d", sprite_x, sprite_y, sprite_w, sprite_h));
  //SaveScreenShot(String.Format("_overlay_%d.bmp", test_render_num));
  //test_render_num++;
  tap.ok(GfxComparer.TakeScreenshotAndMatchSprite(sprite_x, sprite_y, sprite_w, sprite_h, sprite_num, flip), test_name);
}

void _TestObjectRender(Overlay *testObject, int sprite_num, String test_name)
{
  _TestObjectRenderF(testObject, sprite_num, -1, test_name);
}

void TestGraphicPosition(Overlay *testObject, String overlay_name)
{
  testObject.Graphic = SPRITE_DEFAULT;
  _TestObjectRender(testObject, SPRITE_DEFAULT, String.Format("%s: graphic position", overlay_name));
}

void TestOverlayRender()
{
  Overlay* over = Overlay.CreateGraphical(100, 100, 0);
  TestGraphicPosition(over, "Overlay");
  over.Remove();
}

void TestOverlays()
{
  tap.Comment("start Overlays tests");
  tap.Comment(String.Format("System.HardwareAcceleration = %d", System.HardwareAcceleration));
  
  // Disable characters and objects, avoid interfering with our tests
  TestHelpers.HideRoomObjects();
  TestHelpers.HideGUIs();
  
  //------------------------------------------------------------
  // Basic Overlays
  //------------------------------------------------------------
  // Test overlay creation
  TestOverlayCreation();
  // Test graphic overlay's properties and functions
  TestGraphicOverlay();
  // Test textual overlay's properties and functions
  TestTextualOverlay();
  // Test Old Overlay API
  TestOldOverlayAPI();
  
  // Test rendering
  TestOverlayRender();
  
  tap.Comment("end Overlays tests");
}
